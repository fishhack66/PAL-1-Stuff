100 PRINT "            CHUCK-A-LUCK"
105 PRINT "    AN 18TH-CENTURY GAMBLING GAME"
107 PRINT
110 PRINT "ORIGINAL COMPUTER VERSION BY MICHAEL TANOFF"
115 PRINT "ADAPTED TO TINY BASIC BY DAVE HASSLER."
130 PRINT
140 PRINT "CHOOSE A NUMBER FROM 1 TO 6. PAL WILL ROLL 3 DICE."
150 PRINT "IF YOUR NUMBER MATCHES 1 DIE, PAL PAYS OFF EVEN MONEY."
160 PRINT "IF TWO DICE, 2:1, AND IF 3 DICE, 3:1. BET ZERO TO END."
170 PRINT
175 LET M=500
180 PRINT "YOU HAVE $";M;". MAKE A BET:"
190 INPUT B
195 IF B=0 THEN GOTO 460
200 IF B>M THEN GOTO 410
210 IF B>0 THEN GOTO 230
220 GOTO 420
230 PRINT "CHOOSE A NUMBER (1-6);"
240 INPUT N
250 IF N<1 THEN GOTO 260
255 IF N>6 THEN GOTO 260
258 GOTO 270
260 PRINT "DON'T KID A KIDDER, KID..."
265 GOTO 230
270 PRINT "AND THE DICE COME UP..."
272 LET A=RND(6)+1
274 LET D=RND(6)+1
276 LET C=RND(6)+1
280 PRINT A,D,C
290 LET T=0
300 IF A=N THEN LET T=T+1
310 IF D=N THEN LET T=T+1
320 IF C=N THEN LET T=T+1
325 PRINT
330 PRINT "YOU'VE MATCHED ";T;" TIME(S)"
340 IF T=1 THEN GOTO 380
343 IF T=2 THEN GOTO 390
346 IF T=3 THEN GOTO 400
350 PRINT "AND LOSE $";B;"."
360 LET M=M-B
365 IF M<=0 THEN GOTO 430
370 GOTO 180
380 PRINT "AND WIN $";B;"."
383 LET M=M+B
386 GOTO 180
390 PRINT "AND WIN $";B*2;"."
393 LET M=M+(B*2)
396 GOTO 180
400 PRINT "AND WIN $";B*3;"."
403 LET M=M+(B*3)
406 GOTO 180
410 PRINT "CAN'T PLAY WITH WHAT YOU DON'T HAVE, KID."
415 GOTO 180
420 PRINT "VERY FUNNY, KID..."
425 GOTO 180
430 PRINT
440 PRINT "YOU'RE BUSTED! TIME TO GO BACK TO THE FARM, KID!"
450 GOTO 999
460 PRINT "YOU'RE THE DANGDEST SODBUSTER I EVER LAID EYES ON."
470 PRINT "SEE YA IN CHURCH, KID."
999 END
