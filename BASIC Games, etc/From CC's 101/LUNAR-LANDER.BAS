1 PRINT "LUNAR LANDER DEMO BY DAMIAN GARETH WALKER, 2019"
2 REM FOR THE TINY BASIC INTERPRETER AND COMPILER PROJECT 2019
3 REM RELEASED TO THE PUBLIC DOMAIN 2019 BY DGW
4 PRINT "TIDIED UP FOR KIM-1/PAL-1 TINY BASIC BY DAVE HASSLER, 2022"
5 REM --- VARIABLES:
6 REM A: ALT.
7 REM B: FUEL THIS TURN
8 REM F: FUEL LEFT
9 REM T: TIME ELAPSED
10 REM V: VELOCITY THIS TURN
11 REM W: VELO. NEXT
12 REM --- INIT
15 LET A=1000
20 LET B=0
25 LET F=150
30 LET V=50
35 LET T=0
40 PRINT
42 PRINT "THIS IS HOUSTON - LEM IS CLEAR FOR LANDING."
45 REM --- MAIN LOOP
100 PRINT "TIME:";T;"  ALT:";A;"  VELOCITY:";V;"  FUEL:";F;"  THRUST:";B
111 IF F>30 THEN PRINT "THRUST (0-30)";
113 IF F<31 THEN PRINT "THRUST (0-";F;")";
115 INPUT B
117 IF B>=0 THEN IF B<=30 THEN IF B<=F THEN GOTO 120
119 GOTO 111
120 LET W=V-B+5
125 LET F=F-B
130 LET A=A-(V+W)/2
135 LET V=W
140 LET T=T+1
142 PRINT
145 IF A>0 THEN IF F>0 THEN GOTO 100
148 REM --- END
150 IF V<5 THEN IF V>-1 THEN GOTO 165
152 IF F<0 THEN PRINT "OUT OF FUEL..."
155 PRINT "YOU CRASHED!"
160 GOTO 190
165 IF A<0 THEN GOTO 180
170 PRINT "PERFECT LANDING!"
175 GOTO 160
180 PRINT "TOUCHDOWN."
190 IF A<0 THEN LET A=0
195 PRINT "TIME:";T;"  ALT:";A;"  VELOCITY:";V;"  FUEL:";F
196 PRINT "PLAY AGAIN (1=YES)";
197 INPUT C
198 IF C=1 THEN GOTO 15
200 PRINT "THIS IS HOUSTON, SIGNING OFF."
299 END
