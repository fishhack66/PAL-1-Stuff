10 PRINT 
15 PRINT "HURKLE"
20 PRINT "BY BOB ALBRECHT, PEOPLE'S COMPUTER CO."
25 PRINT "ADAPTED TO PAL-1 TINY BASIC BY D.H. HASSLER, 2022"
30 PRINT
110 LET N=5
120 LET G=10
210 PRINT
220 PRINT "A HURKLE IS HIDING ON A ";G;" BY ";G;" GRID. HOMEBASE"
230 PRINT "ON THE GRID IS POINT 0,0 IN THE *SOUTHWEST* CORNER,"
235 PRINT "AND ANY POINT ON THE GRID IS DESIGNATED BY A"
240 PRINT "PAIR OF WHOLE NUMBERS. ENTER THE NUMBERS SEPARATED BY"
242 PRINT "A COMMA, THEN HIT 'RETURN'. THE FIRST NUMBER IS THE"
244 PRINT "HORIZONTAL POSITION AND THE SECOND NUMBER IS THE"
247 PRINT "VERTICAL POSITION."
250 PRINT "YOU MUST TRY TO GUESS THE HURKLE'S GRIDPOINT."
255 PRINT "YOU GET ";N;" TRIES. AFTER EACH TRY, I WILL TELL YOU "
260 PRINT "THE APPROXIMATE DIRECTION TO GO TO LOOK FOR THE HURKLE."
280 PRINT
285 LET A=(RND(10)+1)
286 LET B=(RND(10)+1)
310 LET K=1
320 PRINT "GUESS #";K;"(X,Y)..."
330 INPUT X,Y
340 IF (X-A)+(Y-B)=0 THEN GOTO 500
350 REM PRINT INFO
360 GOSUB 610
370 PRINT
375 LET K=K+1
380 IF K<=N THEN GOTO 320
410 PRINT
420 PRINT "SORRY, THAT'S ";N;" GUESSES."
430 PRINT "THE HURKLE IS AT ";A;",";B
440 PRINT
450 PRINT "PLAY AGAIN? (1=YES)"
452 INPUT C
454 IF C<>1 THEN GOTO 900
456 PRINT "...THE HURKLE IS HIDING..."
460 PRINT
470 GOTO 285
500 REM
510 PRINT
520 PRINT "YOU FOUND HIM IN ";K;" GUESSES!"
540 GOTO 440
610 PRINT "GO ";
620 IF Y=B THEN GOTO 670
630 IF Y<B THEN GOTO 660
640 PRINT "SOUTH";
650 GOTO 670
660 PRINT "NORTH";
670 IF X=A THEN GOTO 720
680 IF X<A THEN GOTO 710
690 PRINT "WEST";
700 GOTO 720
710 PRINT "EAST";
720 PRINT
730 RETURN
900 PRINT "THE HURKLE AWAITS YOU AT ANOTHER TIME...."
910 PRINT "BUT FOR NOW, SO LONG."
999 END
