1 REM ADAPTED FOR KIM TINY BASIC BY D.H. HASSLER
2 REM ORIG BASIC FROM AHL'S 101 BASIC GAMES
5 PRINT
10 PRINT "THE GAME OF HIGH-LOW"
15 PRINT
20 PRINT "YOU HAVE 6 TRIES TO GUESS A DOLLAR AMOUNT"
30 PRINT "BETWEEN 1 AND 100 TO WIN THAT AMOUNT."
40 PRINT "IF YOU WIN, YOU GET ANOTHER PLAY, BUT"
50 PRINT "IF YOU FAIL, THE GAME ENDS, AND YOU"
55 PRINT "WILL BE GIVEN THE OPTION TO PLAY AGAIN."
60 PRINT
160 LET R=0
170 LET B=0
175 PRINT
180 LET Y=RND(100)
200 PRINT "YOUR GUESS"
210 INPUT A
220 LET B=B+1
230 IF A=Y THEN GOTO 300
240 IF A>Y THEN GOTO 270
250 PRINT "THAT IS TOO LOW."
255 GOTO 280
270 PRINT "THAT IS TOO HIGH."
280 PRINT
285 IF B<6 THEN GOTO 200
290 PRINT "YOU BLEW IT! THE JACKPOT WAS:"
292 PRINT Y; " DOLLARS."
295 LET R=0
297 GOTO 350
300 PRINT "THAT'S RIGHT! YOU WIN:"
303 PRINT Y; " DOLLARS."
310 LET R=R+Y
320 PRINT "YOUR TOTAL WINNINGS ARE NOW:"
330 PRINT R; " DOLLARS."
350 PRINT
355 PRINT "PLAY AGAIN? (1=YES & 0=NO)"
360 INPUT Z
370 IF Z=1 THEN GOTO 170
380 PRINT
385 PRINT "BYE! HAVE A BEAUTIFUL TIME!"
390 END
