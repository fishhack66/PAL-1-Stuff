10 PRINT "HAMMURABI - MOD D.HASSLER"
95 LET V=0
97 LET U=0
100 LET Z=0
102 LET P=95
104 LET S=2800
106 LET H=3000
108 LET E=H-S
110 LET Y=3
112 LET A=H/Y
114 LET I=5
116 LET Q=3
210 LET D=0
212 PRINT
215 PRINT " REPORT:"
216 LET Z=Z+1
217 PRINT "IN YR ";Z;", ";D;" STARVED, ";I;" CAME,"
218 LET P=P+I
227 IF Q>2 THEN GOTO 230
228 LET P=(P/3)*2
229 PRINT "PLAGUE! 1/3 DIED!"
230 PRINT "POP: ";P
232 PRINT "CITY: ";A;" ACRES."
235 PRINT "HARVEST: ";Y;" BUSHELS/ACRE."
250 PRINT "RATS ATE ";E;" BU."
260 PRINT "YOU HAVE ";S;" STORED."
265 PRINT
270 IF Z=11 THEN GOTO 860
310 LET C= RND (11)
311 LET Y=C+17
312 PRINT "LAND: ";Y;" BU/ACRE."
315 PRINT "# ACRES TO BUY?"
317 INPUT Q
319 IF Q<0 THEN GOTO 850
322 IF Y*Q<=S THEN GOTO 330
323 GOSUB 710
324 GOTO 315
330 IF Q=0 THEN GOTO 340
331 LET A=A+Q
332 LET S=S-Y*Q
333 LET C=0
334 GOTO 410
340 PRINT "# ACRES TO SELL?"
341 INPUT Q
342 IF Q<0 THEN GOTO 850
344 IF Q<A THEN GOTO 350
346 GOSUB 720
348 GOTO 340
350 LET A=A-Q
360 LET S=S+Y*Q
370 LET C=0
410 PRINT "# BUSH. FOR FOOD?"
411 INPUT Q
412 IF Q<0 THEN GOTO 850
420 IF Q<=S THEN GOTO 430
421 GOSUB 710
422 GOTO 410
430 LET S=S-Q
433 LET C=1
440 PRINT "PLANT # ACRES?"
441 INPUT D
442 IF D=0 THEN GOTO 511
443 IF D<0 THEN GOTO 850
445 IF D<=A THEN GOTO 450
446 GOSUB 720
447 GOTO 440
450 IF D/2<=S THEN GOTO 455
452 GOSUB 710
453 GOTO 440
455 IF D<10*P THEN GOTO 510
460 PRINT "ONLY ";P;" PEOPLE."
470 GOTO 440
510 LET S=S-(D/2)
511 GOSUB 800
515 LET Y=C+1
517 LET H=D*Y
519 LET E=0
521 GOSUB 800
522 IF C/2 <> (C-1)/2 THEN GOTO 530
525 LET E=S/(C+1)
530 LET S=S-E+H
531 GOSUB 800
533 LET I=(((C*20*A+S)/P)/100)+2
540 LET C=Q/20
542 LET Q= RND (18)
550 IF P<C THEN GOTO 210
551 LET D=P-C
552 IF D>((45*P)/100) THEN GOTO 560
553 LET U=((Z-1)*U+D*100/P)/Z
555 LET P=C
557 LET V=V+D
558 GOTO 212
560 PRINT
562 PRINT D;" STARVED!"
565 PRINT "REVOLT!"
567 GOTO 990
710 PRINT "ONLY ";S;" BU."
712 RETURN
720 PRINT "ONLY ";A;" AC."
730 RETURN
800 LET C=RND(6)
801 RETURN
850 PRINT
852 PRINT "I WILL NOT! I QUIT!"
857 GOTO 990
860 PRINT "IN 10 YRS, ";U;"% OF THE"
862 PRINT "PEOPLE STARVED/YR (AVE)."
865 PRINT V;" TOTAL DIED."
867 LET L=A/P
870 PRINT "AT START 10, END WITH ";L;" AC/PERSON."
880 IF U>33 THEN GOTO 565
885 IF L<7 THEN GOTO 565
890 IF U>10 THEN GOTO 940
892 IF L<9 THEN GOTO 940
895 IF U>3 THEN GOTO 960
896 IF L<10 THEN GOTO 960
900 PRINT "MASTERFUL JOB!"
905 GOTO 990
940 PRINT "RANK: DESPOT."
945 GOTO 990
960 PRINT "A FAIR JOB."
990 END
