2 PRINT "DICE"
5 REM ** F = MEMORY LOCATION FOR START OF ARRAY IN DECIMAL
7 REM ** (TOT. MEM. AVAIL: $1780 (6016) TO $17E6 (6118))
10 LET F=6100
12 REM ** P = ADDRESS TO CALL TB'S 'PEEK' ROUTINE
13 LET P=532
15 REM ** O = ADDRESS TO CALL TB'S 'POKE' ROUTINE
16 LET O=536
18 REM   *** GOTO INTRO/INSTRUCTIONS ***
19 GOTO 300
25 REM  *** MAIN LOOP ***
30 LET S=1
40 LET Y=(RND(6)+1)
50 LET Z=(RND(6)+1)
60 LET R=Y+Z
65 REM  ** PEEK RESULT LOCATION, STORE CURR. VALUE IN T
70 LET T=USR(P,F+R)
71 REM  ** POKE THE UPDATED VALUE (T+1) IN RESULT LOC.
72 LET N=USR(O,F+R,T+1)
75 PRINT S,Y,Z
80 LET S=S+1
82 IF S>X THEN GOTO 188
84 GOTO 40
186 REM   ** END MAIN LOOP
187 REM
188 PRINT "    ***** D O N E ! *****"
190 PRINT "ROLL","TOTAL","FREQ."
200 LET V=2
203 LET N=USR(P,F+V)
204 LET W=(N*100)/X
205 PRINT V,N,W;"%"
207 V=V+1
209 IF V>12 THEN GOTO 221
220 GOTO 203
221 PRINT
222 PRINT "ROLL AGAIN? (1=YES)"
223 INPUT A
224 IF A=1 THEN GOTO 380
230 PRINT "BYE."
240 END
300 REM  ** INTRO/INSTR.
320 PRINT "ORIGINAL PROGRAM BY DANNY FREIDUS (WHEN A 7TH GRADER!)"
325 PRINT "ADAPTED FOR KIM-1/PAL-1 TINY BASIC BY D.H. HASSLER, 2022"
326 PRINT
327 PRINT "THE PROGRAM SHOWS USE OF THE USR FUNCTION"
328 PRINT "IN TINY BASIC TO CREATE A ONE-DIMENSIONAL ARRAY."
330 PRINT "IT'S LIMITED: ONLY NUMBERS FROM 0-255 CAN BE IN ONE"
331 PRINT "CELL OF THE ARRAY. CHOOSE 1800 ROLLS TO SEE A ROLLOVER"
332 PRINT "EXAMPLE. 256 SHOULD BE PLENTY FOR DICE, CARD, AND MAZE"
333 PRINT "GAMES, BUT 16-BIT NUMBERS CAN BE HAD BY USING ADJACENT"
334 PRINT "ADDRESSES FOR ONE 'CELL'. ADDITIONAL 'ARRAYS' CAN BE"
335 PRINT "HAD BY DEFINING ADDITIONAL STARTING ADDRESSES. --DHH"
339 PRINT
340 PRINT "THIS PROGRAM SIMULATES THE ROLLING OF A PAIR OF DICE."
350 PRINT "ENTER THE NUMBER OF TIMES YOU WANT THE COMPUTER TO"
360 PRINT "'ROLL' THE DICE AND RESULTS ARE THEN OUTPUT."
365 PRINT "ROLLS HIGHER THAN 1000 TAKE A WHILE, LIKE 5 MINUTES."
375 REM  *** INIT. 'ARRAY' FOR DICE THROW RESULTS ***
380 LET I=1
390 LET N=USR(O,F+I,0)
393 LET I=I+1
396 IF I>12 THEN GOTO 410
400 GOTO 390
405 REM    ** 'ARRAY' NOW ZEROED, 6101 TO 6111 (DECIMAL)
410 PRINT
415 PRINT "HOW MANY ROLLS:"
420 INPUT X
423 IF X<1 THEN GOTO 415
425 GOTO 25

