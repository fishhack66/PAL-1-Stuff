10 PRINT"BOXCARS AND SNAKE EYES - BY DAVE HASSLER, 2022"
30 PRINT"RULES (1=YES)";
40 INPUT Z
50 IF Z=1 THEN GOSUB 700
60 PRINT"COMPUTER'S PLAY:"
70 PRINT"TIMID (1), AVERAGE (2), GREEDY (3)";
80 INPUT S
82 LET H=0
84 LET P=0
88 IF S<1 THEN GOTO 60
90 IF S>3 THEN GOTO 60
92 LET S=S*23
93 IF S=69 THEN GOTO 98
94 PRINT"YOU GO 1ST."
96 LET O=1
97 GOTO 100
98 LET O=2
99 PRINT"I GO 1ST."

100 LET T=0
102 LET J=1
106 PRINT
108 PRINT"SCORE: YOU: ";H,"ME: ";P
110 LET X=(RND(6)+1)
120 LET Y=(RND(6)+1)
130 LET Q=X+Y
133 IF O=1 THEN PRINT"YOUR ";
136 IF O=2 THEN PRINT"MY ";
140 PRINT"ROLL #";J;":  ";X;" & ";Y
150 IF Q=2 THEN GOTO 600
160 IF Q=12 THEN GOTO 500
170 IF Q=7 THEN PRINT" A SEVEN!";
172 IF Q=7 THEN LET Q=-10
175 IF Q=-10 GOTO 220
190 IF X=2 THEN GOTO 210
200 IF Y<>2 THEN GOTO 220
210 LET Q=Q*2
215 PRINT" A TWO!";
220 LET T=T+Q
222 IF T<0 THEN LET T=0
223 PRINT" THAT'S ";Q;" FOR ";T;" PTS."
224 PRINT
225 LET J=J+1
226 IF O=2 THEN IF P+T>=100 THEN GOTO 400
227 IF O=2 THEN IF J>7 THEN LET P=P+T
228 IF O=2 THEN IF J>7 THEN GOTO 505
229 IF O=2 THEN GOSUB 900

230 IF O=2 THEN IF T>=S THEN GOTO 300
235 IF O=2 THEN GOTO 110


236 IF O=1 THEN IF H+T>=100 THEN GOTO 400
237 IF O=1 THEN IF J>7 THEN LET H=H+T
238 IF O=1 THEN IF J>7 THEN GOTO 505

239 IF O=1 THEN IF H>=100 THEN GOTO 400

245 IF O=1 THEN PRINT"ROLL AGAIN (1=YES)";
250 INPUT Z
260 IF Z=1 THEN GOTO 110
270 LET H=H+T
280 LET O=2
290 GOTO 100



300 LET P=P+T
301 PRINT"WORKS FOR ME."
302 IF P>=100 THEN GOTO 400
310 LET O=1
320 GOTO 100

400 IF O=1 THEN IF H<100 THEN LET H=H+T
410 IF O=2 THEN IF P<100 THEN LET P=P+T
420 PRINT"A WINNER!"
430 PRINT"FINAL SCORE: YOU: ";H,"ME: ";P
440 GOTO 620


500 PRINT" *BOXCARS!* NO PTS."
505 IF O=1 THEN GOTO 515
510 IF O=2 THEN LET O=1
512 GOTO 100
515 LET O=2
520 GOTO 100


600 PRINT" *SNAKE EYES!* ZAP! ";
603 IF O=1 THEN PRINT "I ";
606 IF O=2 THEN PRINT "YOU ";
610 PRINT"WIN!"
620 PRINT"PLAY AGAIN (1=YES)";
630 INPUT Z
640 IF Z=1 THEN GOTO 60
650 PRINT"LATER."
660 END


700 PRINT"TWO DICE, 7 ROLLS/TURN MAX., ALT. TURNS."
710 PRINT"DON'T ROLL TO STOP TURN & BANK PTS."
720 PRINT"1ST TO 100 WINS. **THE THROWS:"
730 PRINT"ROLL HAS A 2 = DBL. PTS. (EXCEPT 7)."
740 PRINT"ROLL IS 7 = -10 PTS (ONLY TO 0)"
750 PRINT"ROLL = 'BOXCARS' (12), PTS=0, TURN ENDS."
810 PRINT"ROLL = 'SNAKE EYES' (2), IMMED. LOSS."
830 RETURN

900 LET I=0
910 LET I=I+1
920 IF I<25 THEN GOTO 910
930 RETURN
