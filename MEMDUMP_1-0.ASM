.ORG = $1000

			; $20/21=START DUMP. $22=LINES TO PRINT. 
			; $23/24=TEMP SAVE A,Y. $25/26=HOLDER FOR 16-BIT NUMBER '$0010'
			
	CLD		; INIT
	LDA #$10	; SET UP 16-BIT NUMBER FOR LINE ADDR COUNTER
	STA $25
	LDA #$00
	STA $26
	LDA #$10	; STORE # OF LINES TO PRINT
	STA $22
	JSR $1E2F	; CALL CRLF, PRINT THE TITLE/INSTR.
	LDX #$00
PRNT1	LDA TITLE,X
	BEQ PRSTRT	; OUT TO DUMP
	JSR $1EA0
	INX
	JMP PRNT1

PRSTRT	LDA $21		; PRINT THE HEX VALUE OF START (HI/LO)
	JSR PRHEX	; PRINT A HEX BYTE
	LDA $20
	JSR PRHEX
	JSR PRTSPC	; CALL OUTSP
	LDY #$00	; START 'PRINT-A-HEX-LINE' LOOP
LOOP1	LDA ($20),Y
	JSR PRHEX	; PRINT A HEX BYTE
	JSR PRTSPC	; THEN A SPACE
	INY
	CPY #$10	; IS IT 16 YET?
	BNE LOOP1	; NO = LOOP TO FINISH LINE
	LDY #$00	; START 'PRINT-ASCII-LINE' LOOP
LOOP2	LDA ($20),Y
	JSR PRASCI	; PRINT AN ASCII BYTE
	INY
	CPY #$10	; IS IT 16 YET?
	BNE LOOP2	; NO = LOOP UNTIL FINISH LINE

FINLIN	JSR CRLF	; CALL CRLF	



	CLC		; ADD $10 TO THE ADDRESS-TO-DUMP
	LDA $20		; LSB FIRST...
	ADC $25
	STA $20
	LDA $21		; ... THEN MSB.
	ADC $26
	STA $21


	SEC
	LDA $22		; DECRIMENT THE LINES-TO-PRINT COUNTER
	SBC #$01	; BY SUBTRACTING 1
	STA $22		; STORE NEW COUNTER
	BNE PRSTRT	; IF NOT 0 THEN GOTO PRSTRT
	BRK		; QUIT IF LAST LINE PRINTED
	

PRASCI	JSR STORAY	; STORE A, Y
        CMP #$20	; IS AC < $20/32 DEC?
	BCC PRTDOT	; YES - GO PRINT A PERIOD
	CMP #$7F	; IS AC > 127?
	BCS PRTDOT	; YES - GO PRINT A PERIOD
PRNTIT	JSR $1EA0	; NO - CALL OUTCH
	JMP RESTOR  	; JMP TO RESTORE A,Y     
PRTDOT	LDA #$2E	; PRINT A TRADITIONAL '.' FOR NON-PRINTING ASCII
	JMP PRNTIT


PRHEX	JSR STORAY	; STORE A, Y
	JSR $1E3B	; CALL PRTBYT
	JMP RESTOR  	; JMP TO RESTORE A,Y     

PRTSPC	JSR STORAY	; STORE A, Y
	JSR $1E9E	; CALL OUTSP
	JMP RESTOR  	; JMP TO RESTORE A,Y     	

CRLF 	STA $23  	; START OF PRINT 2 HEX CHARS SUBROUTINE
	STY $24		; BY STORING A,Y                        
	JSR $1E2F 	; CALL OUTCH                     
RESTOR	LDA $24         ; RESTORE Y,A
	TAY
	LDA $23	
	RTS		

STORAY	STA $23		; STORE A, Y
	TYA             
	STA $24
	LDA $23
	RTS


TITLE
.BYTE $4C $49 $4C $27 $20 $4D $45 $4D
.BYTE $20 $44 $55 $4D $50 $20 $2D $20
.BYTE "Low byte in $20; high byte in $21."
.BYTE $0A $0D $00 $4B $37 $43 $43 $43
